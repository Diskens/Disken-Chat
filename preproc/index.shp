@doctype
$html {
  $head {
    %meta[charset 'utf-8']
    $title { Disken Chat }
    %link[rel stylesheet href
      'http://fonts.googleapis.com/css?family=Lato:300,400,700']
    %link[rel icon type image/png href 'favicon.ico']
    %link[rel stylesheet href 'index.css']

    $script[src '/socket.io/socket.io.js']
    $script[src 'utilities/Domi.js']
    $script[src 'utilities/Popup.js']
    $script[src 'utilities/Cookie.js']

    $script[src 'room/Room.js']
    $script[src 'room/EntryTypes.js']
    $script[src 'room/EntriesManager.js']
    $script[src 'room/UploadManager.js']
    $script[src 'room/MembersManager.js']
    $script[src 'room/RoomDomManager.js']

    $script[src 'app/RoomsManager.js']
    $script[src 'app/UserManager.js']

    $script[src 'Main.js']
  }
  $body[onload 'onDomLoaded()'] {
    $div[.Background]
    $div[#PopupBox]

    $header[#AnonHeader] {
      $button[#BtnHome onclick 'swMain.goto("landing")'] {Home}
      $button[#BtnAbout onclick 'swMain.goto("about")'] {About}
      $span[.Version]
      $div[.Clear]
    }
    $header[#UserHeader] {
      $button[#BtnRoom onclick 'swMain.goto("room")'] {Rooms}
      $button[#BtnBrowse onclick 'swMain.goto("browse")'] {Browse}
      $button[#BtnCreate onclick 'swMain.goto("create")'] {Create}
      $button[#BtnAccount onclick 'swMain.goto("account")'] {Account}
      $button[#BtnLogOut .redText onclick 'USER.logout()'] {Log out}
      $div[.Clear]
    }

    $section[#Banner] {
      $h1 {Disken Chat}
      $div {Room-based instant messaging app}
    }

    $section[#SectionLanding .TextContent .SlideIn !hidden] {
      $div {Create your account or log in to continue!}
      $form[#LoginForm action 'javascript:void(0);'] {
        %input[#LoginUsername type text placeholder Username]
        %input[#LoginPassword type password placeholder Password]
        $button[onclick 'USER.credsLogin()'] {Log in}
      }
      $form[#SignupForm action 'javascript:void(0);'] {
        %input[#SignupUsername type text placeholder Username]
        %input[#SignupEmail type email placeholder you@example.com]
        %input[#SignupPassword type password placeholder Password]
        $button[onclick 'USER.signup()'] {Sign up}
      }
      $div[.Clear]
    }

    $section[#SectionAbout .TextContent .SlideIn] {
      $h2 {About}
      $h4 {Features} $div[.text] {
        This app lets you chat with your friends in chat room environment.
        You can send text, links and images in real time.
        The latest version also lets you react to messages you received.
      } $div[.text] {
        Room access code is required to join the room.
        You can find it in the details tab.
        Room owner can reset the code if desired.
      }
      $h4 {Development} $div[.text] {
        This app runs on a Node.js Express server and uses MongoDB.
        Real-time communication was achieved with Socket.io package.
        The HTML code was generated by my own SHP preprocessor.
        CSS was generated by the SASS preprocessor.
        Single-document app functionalities were developed with Domi.js package.
      } $div[.text] {
        Currently running version $span[.Version]
      }
    }

    $section[#SectionAccount .TextContent .SlideIn] {
      Your account details
    }

    $section[#SectionRoom] {
      $header[#RoomHeader] {
        $button[#BtnChat onclick 'swRoom.goto("chat")'] {Chat}
        $button[#BtnDetails onclick 'swRoom.goto("details")'] {Details}
        $button[#BtnList .redText onclick 'ROOMS.leaveRoom()'] {Back}
        $div[.Clear]
      }
      $div[#RoomList .TextContent .SlideIn] {
        $div[#RoomListHeader] {
          $div[.text] {List of your rooms}
          $button[onclick 'ROOMS.getUserRooms(true)'] {Refresh}
          $div[.Clear]
        }
        $div[#RoomListContainer]
      }
      $div[#RoomChat]
      $div[#RoomDetails .TextContent .SlideIn]
    }

    $section[#SectionBrowse] {
      $header {
        $button[#BtnPublic onclick 'swBrowse.goto("browse")'] {Browse}
        $button[#BtnJoin onclick 'swBrowse.goto("join")'] {Join}
        $div[.Clear]
      }
      $div[#BrowsePublic .TextContent .SlideIn] {
        $div {Browse public rooms}
      }
      $div[#BrowseJoin .TextContent .SlideIn] {
        $div {Join a room}
        $form[#JoinForm action 'javascript:void(0);'] {
          %input[#JoinName type text placeholder 'Room name']
          %input[#JoinPasscode type text placeholder 'Room passcode']
          $button[onclick 'ROOMS.joinRoom();'] {Confirm}
        }
      }
    }

    $section[#SectionCreate .TextContent .SlideIn] {
      $div {Create new room}
      $form[#CreateForm action 'javascript:void(0);'] {
        %input[#CreateName type text placeholder 'Room name']
        $label { Make this room visible in the search results
          %input[#CreateMakeVisible type checkbox !checked]
          $span[.CustomCheckbox]}
        $label { Require passcode to join this room
          %input[#CreateRquirePasscode type checkbox !checked]
          $span[.CustomCheckbox]}
        $button[onclick 'ROOMS.createRoom();'] {Confirm}
      }
    }
  }
}
